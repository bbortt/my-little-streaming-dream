plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE' apply false
}

group = 'io.github.bbortt.qdrakeboo'

allprojects {
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    def globalJavaConfiguration = {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'

        sourceCompatibility = '11'

        dependencies {
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
        }
    }

    pluginManager.withPlugin('java', globalJavaConfiguration)
    pluginManager.withPlugin('java-library', globalJavaConfiguration)

    pluginManager.withPlugin('java-library') {
        jar {
            enabled = true

            from {
                configurations.compileClasspath.filter { it.exists() }.collect {
                    it.isDirectory() ? it : zipTree(it)
                }
            }
        }

        bootJar {
            enabled = false
        }
    }

    if (name.startsWith('qdrakeboo-model')) {
        apply plugin: 'java-library'

        dependencies {
            implementation 'javax.persistence:javax.persistence-api'
            implementation 'org.apache.commons:commons-lang3'
            implementation 'org.hibernate:hibernate-core'
        }
    }
}
