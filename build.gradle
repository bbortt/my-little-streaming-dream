plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE' apply false
    id 'org.sonarqube' version '2.8'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

group = 'io.github.bbortt.qdrakeboo'

ext {
    set('springBootVersion', "2.2.1.RELEASE")
    set('springCloudVersion', "Hoxton.RC2")
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'bbortt_qdrakeboo'
    }
}

allprojects {
    version "${version}"

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" } // Remove when using set('springCloudVersion', "Hoxton.RELEASE")
    }
}

subprojects {
    def globalJavaConfiguration = {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'jacoco'

        buildscript {
            dependencies {
                classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
            }
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }

        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }

        check.dependsOn jacocoTestReport
    }

    pluginManager.withPlugin('java', globalJavaConfiguration)
    pluginManager.withPlugin('java', {
        configurations {
            developmentOnly

            runtimeClasspath {
                extendsFrom developmentOnly
            }
        }

        dependencies {
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-config'
        }
    })
}
